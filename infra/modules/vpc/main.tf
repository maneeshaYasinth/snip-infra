data "aws_availability_zones" "available" {
  state = "available"
}

resource "aws_vpc" "main" {
  cidr_block = var.vpc_cidr
  enable_dns_hostnames = true
  enable_dns_support = true

  tags = {
    Name        = "${var.project}-vpc"
    Project     = var.project
    Environment = var.environment
  }
}

#------public subnets for ALB------
resource "aws_subnet" "public" {
  count = 2
  vpc_id = aws_vpc.main.id
  cidr_block = cidrsubnet(var.vpc_cidr, 4, count.index)
  availability_zone = data.aws_availability_zones.available.names[count.index]

  tags = {
    Name        = "${var.project}-public-subnet-${count.index + 1}"
    Project     = var.project
    Environment = var.environment
    Tier        = "public"
  }
}

#------private subnets for ECS tasks------
resource "aws_subnet" "private" {
    count = 2
    vpc_id = aws_vpc.main.id
    cidr_block = cidrsubnet(var.vpc_cidr, 4, count.index + 2)
    availability_zone = data.aws_availability_zones.available.names[count.index]
    
    tags = {
        Name        = "${var.project}-private-subnet-${count.index + 1}"
        Project     = var.project
        Environment = var.environment
    }
}

#------Internet Gateway------
resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "${var.project}--igw"
    Project     = var.project
    Environment = var.environment
  }
}

#----NAT Gateway------
resource "aws_eip" "nat" {
  domain = "vpc"

  tags = {
    Name = "${var.project}--nat-eip"
    Project     = var.project
    Environment = var.environment
  }
}

resource "aws_nat_gateway" "main" {
  allocation_id = aws_eip.nat.id
  subnet_id = aws_subnet.public[0].id

  tags = {
    Name = "${var.project}--nat-gateway"
    Project     = var.project
    Environment = var.environment
  }

depends_on = [ aws_internet_gateway.main ]

}

#------Route Table for public subnets------

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id

route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
}

    tags = {
        Name = "${var.project}--public-rt"
        Project     = var.project
        Environment = var.environment
    }

}

resource "aws_route_table_association" "public" {
  count = 2
  subnet_id = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public.id
}

#------Route Table for private subnets------

resource "aws_route_table" "private" {
  vpc_id = aws_vpc.main.id

route {
    cidr_block = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.main.id 
}

    tags = {
        Name = "${var.project}--private-rt"
        Project     = var.project
        Environment = var.environment
    }

}

resource "aws_route_table_association" "private" {
  count = 2
  subnet_id = aws_subnet.private[count.index].id
  route_table_id = aws_route_table.private.id
}